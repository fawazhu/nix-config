#!/usr/bin/zsh

alias fzf="fzf-tmux"

export FZF_PREVIEW='if ! bat -n --color=always {} 2>/dev/null;then;eza -T --color=always --icons {} 2>/dev/null;fi'
export FZF_DEFAULT_OPTS="--layout=reverse --preview='${FZF_PREVIEW}' --preview-window='right:60%:nohidden'"
export FZF_DEFAULT_COMMAND="fd --hidden --follow --exclude '.git/' --exclude 'node_modules' --exclude '.terraform'"

# CTRL-T - Paste the selected file path(s) into the command line
__fsel() {
  local cmd="${FZF_DEFAULT_COMMAND}"
  setopt localoptions pipefail 2> /dev/null
  local item
  eval "$cmd" | fzf-tmux -m "$@" | while read item; do
    echo -n "${(q)item} "
  done
  local ret=$?
  return $ret
}

fzf-file-widget() {
  LBUFFER="${LBUFFER}$(__fsel)"
  local ret=$?
  zle reset-prompt
  return $ret
}
zle     -N            fzf-file-widget
bindkey -M emacs '^T' fzf-file-widget
bindkey -M vicmd '^T' fzf-file-widget
bindkey -M viins '^T' fzf-file-widget

# CTRL-R - Paste the selected command from history into the command line
fzf-history-widget() {
  local selected num
  setopt localoptions noglobsubst noposixbuiltins pipefail no_aliases 2> /dev/null
  selected=( $(fc -rl 1 | awk '{ cmd=$0; sub(/^[ \t]*[0-9]+\**[ \t]+/, "", cmd); if (!seen[cmd]++) print $0 }' |
  FZF_DEFAULT_OPTS="-n2..,.. --scheme=history ${FZF_DEFAULT_OPTS} --query=${(qqq)LBUFFER} +m --preview-window='right:0%:hidden' --layout=reverse " fzf-tmux) )
  local ret=$?
  if [ -n "$selected" ]; then
    num=$selected[1]
    if [ -n "$num" ]; then
      zle vi-fetch-history -n $num
    fi
  fi
  zle reset-prompt
  return $ret
}
zle     -N            fzf-history-widget
bindkey -M emacs '^R' fzf-history-widget
bindkey -M vicmd '^R' fzf-history-widget
bindkey -M viins '^R' fzf-history-widget

get_proj() {
  local base_dir="${HOME}/git"
  if [[ ! -d "${base_dir}" ]]; then
    echo "$0: ${base_dir} does not exist." 1>&2
    return 1
  fi
  local dir_with_git
  if ! dir_with_git="$(fd --base-directory="${base_dir}" -Hpu '^.*\.git$')"; then
    echo "$0: failed to search in ${base_dir}. Is fd installed?" 1>&2
    return 2
  fi
  dir_with_git="$(echo -n "${dir_with_git}" | sed -e 's#/\.git/##')"
  if [[ "${dir_with_git}" == "" ]]; then
    echo "$0: no git projects in ${base_dir}." 1>&2
    return 3
  fi
  local dir
  local fzf_preview="eza -T --color=always --icons ${base_dir}/{}"
  local FZF_DEFAULT_OPTS="--layout=reverse --preview='${fzf_preview}' --preview-window='right:60%'"
  local chosen_dir
  if ! chosen_dir="$(echo -n "${dir_with_git}" | fzf-tmux)"; then
    echo "$0: no directory chosen." 1>&2
    return 4
  fi
  echo "${base_dir}/${chosen_dir}"
}

proj() {
  local dir
  if ! dir="$(get_proj)"; then
    return $?
  fi
  if ! cd "${dir}"; then
    echo "$0: failed to change directory to ${dir}." 1>&2
    return 5
  fi
}

proj_bind() {
  local dir
  if ! dir="$(get_proj)"; then
    return $?
  fi
  zle push-line
  BUFFER="cd \"${dir}\""
  zle accept-line
  zle reset-prompt
}
zle     -N            proj_bind
bindkey -M emacs '^P' proj_bind
bindkey -M vicmd '^P' proj_bind
bindkey -M viins '^P' proj_bind
